module Test.QuickCheck.Testable;

open import Stdlib.Prelude;
open import Data.Random;
open import Data.Showable;
open import Test.QuickCheck.Arbitrary;
open import Test.QuickCheck.CoArbitrary;
open import Test.QuickCheck.Result;
open import Test.QuickCheck.Property;
open import Test.QuickCheck.Gen;

--- A conversion from a Type A to a ;Property;
type Testable (A : Type) :=
  | testable : (A -> Property) -> Testable A;

toProp : {A : Type} -> Testable A -> A -> Property;
toProp (testable f) := f;

forAll :
  {A : Type}
    -> {T : Type}
    -> Showable A
    -> Testable T
    -> Gen A
    -> (A -> T)
    -> Property;
forAll {A} {T} (showable show) (testable asProp) g prop :=
  property
    (gen
      \ {
        | rand := let
          srand : StdGen Ã— StdGen := stdSplit rand;
          rand1 : StdGen := fst srand;
          rand2 : StdGen := snd srand;
          arg : A := runGen g rand1;
          subProp : Property := asProp (prop arg);
          res : Result := runProp subProp rand2;
        in overFailure
          res
          \ {
            | (failure s cs) := failure s (show arg :: cs)
            | s := s
          }
      });

testableProperty : Testable Property;
testableProperty := testable id;

testableResult : Testable Result;
testableResult :=
  testable
    \ {
      | r := property (gen (const r))
    };

testableBool : Testable Bool;
testableBool :=
  let
    toResult : Bool -> Result;
    toResult b := if b success (failure 0 nil);
  in testable
    \ {
      | b := toProp testableResult (toResult b)
    };

testableFunction :
  {A : Type}
    -> {T : Type}
    -> Showable A
    -> Arbitrary A
    -> Testable T
    -> Testable (A -> T);
testableFunction s@(showable show) (arbitrary g) t@(testable asProp) :=
  testable
    \ {
      | f := forAll s t g f
    };

testableBinaryInt : Testable (Int -> Int -> Bool);
testableBinaryInt :=
  testableFunction
    showableInt
    arbitraryInt
    (testableFunction showableInt arbitraryInt testableBool);

testableListInt : Testable (List Int -> Bool);
testableListInt :=
  testableFunction
    (showableList showableInt)
    (arbitraryList arbitraryInt)
    testableBool;

testableListIntListInt :
  Testable (List Int -> List Int -> Bool);
testableListIntListInt :=
  testableFunction
    (showableList showableInt)
    (arbitraryList arbitraryInt)
    testableListInt;

testableNatListInt : Testable (Nat -> List Int -> Bool);
testableNatListInt :=
  testableFunction showableNat arbitraryNat testableListInt;

testableHofIntBool : Testable ((Int -> Bool) -> Bool);
testableHofIntBool :=
  let
    arb : Arbitrary (Int -> Bool);
    arb := arbitraryFunction coarbitraryIntBool arbitraryBool;
  in testableFunction showableFunction arb testableBool;

testableListIntHofIntBool :
  Testable (List Int -> (Int -> Bool) -> Bool);
testableListIntHofIntBool :=
  testableFunction
    (showableList showableInt)
    (arbitraryList arbitraryInt)
    testableHofIntBool;

testableHofIntInt : Testable ((Int -> Int) -> Bool);
testableHofIntInt :=
  let
    arb :
        Arbitrary (Int -> Int) :=
          arbitraryFunction coarbitraryIntInt arbitraryInt;
  in testableFunction showableFunction arb testableBool;

testableIntIntHofIntInt :
  Testable (Int -> Int -> (Int -> Int) -> Bool);
testableIntIntHofIntInt :=
  testableFunction
    showableInt
    arbitraryInt
    (testableFunction
      showableInt
      arbitraryInt
      testableHofIntInt);
